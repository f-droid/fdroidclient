plugins {
    id 'org.jetbrains.kotlin.multiplatform'
    id 'com.android.library'
    id 'org.jetbrains.dokka'
    id 'com.vanniktech.maven.publish'
}

kotlin {
    androidTarget {
        compilations.configureEach {
            kotlinOptions.jvmTarget = '17'
        }
        publishLibraryVariants("release")
    }
//    def hostOs = System.getProperty("os.name")
//    def isMingwX64 = hostOs.startsWith("Windows")
//    def nativeTarget
//    if (hostOs == "Mac OS X") nativeTarget = macosX64('native')
//    else if (hostOs == "Linux") nativeTarget = linuxX64("native")
//    else if (isMingwX64) nativeTarget = mingwX64("native")
//    else throw new GradleException("Host OS is not supported in Kotlin/Native.")

    sourceSets {
        configureEach {
            languageSettings {
                optIn('kotlin.RequiresOptIn')
                explicitApi('strict')
            }
        }
        commonMain {
            dependencies {
                api libs.ktor.client.core
                implementation libs.microutils.kotlin.logging
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
                implementation libs.ktor.client.mock
                implementation libs.mockk
            }
        }
        // JVM is disabled for now, because Android app is including it instead of Android library
        jvmMain {
            dependencies {
                implementation libs.ktor.client.cio
            }
        }
        jvmTest {
            dependencies {
                implementation libs.junit
            }
        }
        androidMain {
            dependencies {
                implementation libs.ktor.client.okhttp
                //noinspection UseTomlInstead
                implementation("com.github.bumptech.glide:glide:4.16.0") {
                    transitive = false // we don't need all that it pulls in, just the basics
                }
                implementation libs.glide.annotations
            }
        }
        androidUnitTest {
            dependencies {
                implementation kotlin('test')
                implementation libs.json
                implementation libs.junit
                implementation libs.logback.classic
            }
        }
        androidInstrumentedTest {
            dependsOn(commonTest)
            dependencies {
                implementation project(":libs:sharedTest")
                implementation libs.androidx.test.runner
                implementation libs.androidx.test.ext.junit
                implementation libs.mockk.android
            }
        }
        nativeMain {
            dependencies {
                implementation libs.ktor.client.curl
            }
        }
        nativeTest {

        }
    }
}

android {
    namespace "org.fdroid.download"
    compileSdk libs.versions.compileSdk.get().toInteger()
    sourceSets {
        main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
        getByName("androidTest").java.srcDir(file("src/androidAndroidTest/kotlin"))
    }
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 34 // needed for instrumentation tests
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunnerArguments disableAnalytics: 'true'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    lintOptions {
        checkReleaseBuilds false
        abortOnError true

        htmlReport true
        xmlReport false
        textReport true

        lintConfig file("lint.xml")
    }
    testOptions {
        packaging {
            resources.excludes.add("META-INF/*")
        }
    }
}

signing {
    useGpgCmd()
}

import org.jetbrains.dokka.gradle.DokkaTask
tasks.withType(DokkaTask).configureEach {
    pluginsMapConfiguration.set(
            ["org.jetbrains.dokka.base.DokkaBase": """{
                "customAssets": ["${file("${rootProject.rootDir}/logo-icon.svg")}"],
                "footerMessage": "Â© 2010-2022 F-Droid Limited and Contributors"
            }"""]
    )
}
