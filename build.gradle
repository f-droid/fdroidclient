buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.android.tools.build:gradle:0.8.+'
	}
}

apply plugin: 'android'

dependencies {
	compile files('libs/android-support-v4.jar')
	compile project(':extern:AndroidPinning')
	compile project(':extern:UniversalImageLoader:library')
	compile project(':extern:MemorizingTrustManager')
}

subprojects {
	buildscript {
		repositories {
			mavenCentral()
		}
		dependencies {
			classpath 'com.android.tools.build:gradle:0.8.+'
		}
	}
	apply plugin: 'android-library'
	android {
		buildToolsVersion '19.0.2'
		packagingOptions {
			exclude "META-INF/LICENSE*"
			exclude "META-INF/NOTICE*"
			exclude "META-INF/README*"
			exclude "META-INF/CHANGELOG*"
			exclude "META-INF/BUILD*"
		}
	}
}

project(':extern:UniversalImageLoader:library') {
	android {
		compileSdkVersion 16

		sourceSets {
			main {
				manifest.srcFile 'AndroidManifest.xml'
				java.srcDirs = ['src']
				resources.srcDirs = ['src']
				aidl.srcDirs = ['src']
				renderscript.srcDirs = ['src']
			}
		}
	}
}

android {
	compileSdkVersion 19
	buildToolsVersion '19.0.2'

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}

		instrumentTest.setRoot('test')
	}

	buildTypes {
		release {
			runProguard true
			proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-project.txt'
		}
	}

	tasks.withType(Compile) {
		options.encoding = "UTF-8"
	}
}
